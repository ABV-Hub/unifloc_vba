'=======================================================================================
'Unifloc 7.9  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
Option Explicit
' Gaslift valve description
Public h_mes_m As Double            ' глубина установки газлифтного клапана
Public p_bellow_sc_atma As Double   ' давление зарядки сильфона на поверхности на поверхности
Public p_in_atma As Double          ' casing pressure at gas valve
Public p_v_atma As Double           ' pressure between inlet and outlet
Public p_out_atma As Double         ' tubing pressure at gas valve
Public t_C As Double             ' casing temperature at gas valve
Public q_gas_inj_scm3day As Double  ' gas rate through valve
Public crit_flow As Double
Public p_crit_atma As Double
Public q_gas_max_sm3day As Double
' IPO valve data
' for now supposed Wheatherford R-1 data
Public r As Double
Public dext_mm As Double
Public Ab_mm2 As Double
Public Ap_mm2 As Double
Public PREF As Double
'Public dport_mm As Double
Public IPO As Boolean
Private p_d_vkr_mm() As Double
Private p_d_vkr_eff_mm As Double
Private p_d_port_mm As Double             ' диаметр порта
Public fluid As CPVT                 ' gas properties
' задаем характеристики клапана R-1 с учетом наличия вкруток
Public Sub set_GLV_R1(Optional ByVal IPO As Boolean = False, _
                      Optional port_mm As Double = R1_port_1_8, _
                      Optional d_vkr1_mm As Double = -1, _
                      Optional d_vkr2_mm As Double = -1, _
                      Optional d_vkr3_mm As Double = -1, _
                      Optional d_vkr4_mm As Double = -1)
' set R-1 valve data
    Dim Ap_Ab As Double
    Dim i As Integer
    
    Me.IPO = IPO
    
    p_d_port_mm = port_mm
    dext_mm = 25.4
    Ab_mm2 = 200
    Select Case port_mm
        Case R1_port_1_4
            Ap_mm2 = 8.4
        Case R1_port_5_32
            Ap_mm2 = 13.55
        Case R1_port_3_16
            Ap_mm2 = 18.71
        Case R1_port_1_8
            Ap_mm2 = 33.55
        Case R1_port_5_16
            Ap_mm2 = 51.61
        Case Else
            Ap_mm2 = (port_mm ^ 2) * (1 - 0.16)  ' approximation based on R-1 table
    End Select
    Ap_Ab = Ap_mm2 / Ab_mm2
    r = Ap_Ab
    PREF = Ap_Ab / (1 - Ap_Ab)
    ' estimate effective diameter with "vkrutka"
    Dim num_vkr As Integer
    num_vkr = 0
    If d_vkr1_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve p_d_vkr_mm(1 To num_vkr)
        p_d_vkr_mm(num_vkr) = d_vkr1_mm
    End If
    If d_vkr2_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve p_d_vkr_mm(1 To num_vkr)
        p_d_vkr_mm(num_vkr) = d_vkr2_mm
    End If
    If d_vkr3_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve p_d_vkr_mm(1 To num_vkr)
        p_d_vkr_mm(num_vkr) = d_vkr3_mm
    End If
    If d_vkr4_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve p_d_vkr_mm(1 To num_vkr)
        p_d_vkr_mm(num_vkr) = d_vkr4_mm
    End If
    p_d_vkr_eff_mm = 0
    For i = LBound(p_d_vkr_mm) To UBound(p_d_vkr_mm)
        p_d_vkr_eff_mm = p_d_vkr_eff_mm + (p_d_vkr_mm(i)) ^ 2
    Next i
    p_d_vkr_eff_mm = p_d_vkr_eff_mm ^ 0.5
    
End Sub
Public Property Get d_port_mm() As Double
    d_port_mm = p_d_port_mm
End Property
Public Property Let d_port_mm(val As Double)
    p_d_port_mm = val
End Property
Public Property Get p_open_atma() As Double
    If IPO Then
        ' for opening assume p_v_atma = p_out_atma
        p_open_atma = p_close_atma / (1 - r) - p_out_atma * r / (1 - r)
    Else
        p_open_atma = 1
    End If
End Property
Public Property Get p_close_atma() As Double
    If IPO Then
        p_close_atma = GLV_p_close_atma(p_bellow_sc_atma, t_C)
    Else
        p_close_atma = 1
    End If
End Property
' функция расчета расхода газа через клапан
Public Function calc_q_gas_sm3day(Optional p_intake_atma As Double = -1, _
                             Optional p_out_atma As Double = -1, _
                             Optional t_in_C As Double = -1) As Double
    Dim rslt
    
    p_in_atma = p_intake_atma
    Me.p_out_atma = p_out_atma
    
    If p_intake_atma < 0 Then p_intake_atma = Me.p_in_atma
    If p_out_atma < 0 Then p_out_atma = Me.p_out_atma
    If t_in_C < 0 Then t_in_C = Me.t_C
    
    If (p_out_atma < p_intake_atma) And (p_d_port_mm > 0) And (p_d_vkr_eff_mm = 0) Then
        calc_q_gas_sm3day = GLV_q_gas_sm3day(p_d_port_mm, p_intake_atma, p_out_atma, fluid.gamma_g, t_in_C)
        p_v_atma = p_out_atma
    ElseIf (p_out_atma < p_intake_atma) And (p_d_port_mm > 0) And (p_d_vkr_eff_mm > 0) Then
        rslt = GLV_q_gas_vkr_sm3day(p_d_port_mm, p_d_vkr_eff_mm, p_intake_atma, p_out_atma, fluid.gamma_g, t_in_C)
        calc_q_gas_sm3day = rslt(0)
        p_v_atma = rslt(2)
    End If
    
    If IPO Then
    ' need check open condition
        If p_intake_atma < p_close_atma Then
            calc_q_gas_sm3day = 0
        End If
    End If
    
End Function
Public Function calc_p_out_atma(p_intake_atma As Double, q_gas_scm3day As Double)
    Me.p_out_atma = p_out_atma
    ' ищем давление внутри клапана
    If p_d_vkr_eff_mm > 0 Then
        p_v_atma = GLV_p_atma(p_d_vkr_eff_mm, p_intake_atma, q_gas_scm3day, fluid.gamma_g, t_C, True)
    Else
        p_v_atma = p_out_atma
    End If
    p_out_atma = GLV_p_atma(p_d_port_mm, p_v_atma, q_gas_scm3day, fluid.gamma_g, t_C, True)
    calc_p_out_atma = p_out_atma
    
End Function
' расчет давления на входе в клапан (затрубное давление)
' по давлению в НКТ
Public Function calc_p_in_atma(p_out_atma As Double, q_gas_scm3day As Double)
    Me.p_out_atma = p_out_atma
    ' ищем давление внутри клапана
    If p_d_vkr_eff_mm > 0 Then
        p_v_atma = GLV_p_atma(p_d_vkr_eff_mm, p_out_atma, q_gas_scm3day, fluid.gamma_g, t_C, False)
    Else
        p_v_atma = p_out_atma
    End If
    p_in_atma = GLV_p_atma(p_d_port_mm, p_v_atma, q_gas_scm3day, fluid.gamma_g, t_C, False)
    calc_p_in_atma = p_in_atma
End Function
