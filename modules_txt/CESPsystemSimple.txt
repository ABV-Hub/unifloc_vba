'=======================================================================================
'Unifloc 7.25  coronav                                          khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
'
' класс описывает систему ЭЦН в простом варианте - один ЭЦН+ПЭД+кабель
' класс отвечатает и за расчет сепарации и свойств флюидов
Option Explicit
Private ESP_pump_ As CESPpump  ' сам насос
Public ESP_motor As New CESPMotor
' описание газосепаратора
Public isGasSep As Boolean
Public ksep_gassep_fr As Double           ' коэффициент сепарации газосепаратора
Public ksep_manual_fr As Double
Public dPower_GasSep_W As Double
' параметры сепарации газа на приеме насоса
' при расчете снизу вверх могут быть рассчитаны по корреляциям
' могут быть использованы значения заданные вручную
' при расчете сверху вниз значения параметров сепарации должны быть заданы вручную
' должны быть заданы и значения давления и температуры сепарации
Public isManualSeparationCalc As SEPAR_CALC_METHOD   ' флаг для ручного задания параметров сепарации
                                           ' 0 - нет - расчет автоматический
                                           ' 1 - Ксеп ручной, давления автомат
                                           ' 2 - Ксеп ручной, давления и температура ручные
Public ksep_nat_fr As Double
' property ksep_total_fr As Double  defined below
' для расчета сепарации надо либо задать, либо спасти значения при которых проводился расчет сепарации
Public p_ksep_atma As Double
Public t_ksep_C As Double
Public t_int_C As Double
Public t_dis_C As Double
Public p_dis_atma As Double
Public p_int_atma As Double
' простой набор параметров для описания УЭЦН
Public U_V As Double                ' напряжение на ПЭД фактическое
Private U_trans_high_V_ As Double    ' напряжение на трансформатере на высокой стороне, расчетное
Private U_trans_low_V_ As Double     ' напряжение на трансформатере на низкой стороне, константа по факту
                                    ' todo логично было бы задавать напряжение на поверхности, а считать какое
                                    ' будет внизу - но это усложняе расчет и отложено на потом
Public power_motor_nom_W As Double  ' номинальная мощность ПЭД на номинальной частоте
Private power_motor_W_ As Double       ' расчетная мощность на ПЭД, только читать
Private power_CS_fact_W_ As Double  ' фактическая мощность системы замеренная на СУ, задается для коррекции мощности
Private energy_fact_Whday_ As Double  ' фактическое потребление энергии за сутки Втч/день
Public freq_nom_Hz As Double        ' номинальная частота вращения ЭЦН из базы ЭЦН
Public motor_eff_fr As Double       ' номинальный КПД ПЭД todo заменить на характеристик ПЭД
Public CS_eff_fr As Double          ' номинальный КПД станции управления УЭЦН
Public Transform_eff_fr As Double   ' номинальный КПД трансформатора УЭЦН
Public dPower_CS_W As Double        ' потребление мощности станцией управления
Public dPower_transform_W As Double ' потребление мощности трансформатором
'Public dPower_protector_W As Double ' потребление мощности гидрозашитой
Public power_shaft_W As Double      ' мощность механическая на валу
Public power_CS_calc_W As Double        ' мощность на входе в станцию управления (с учетом корректировки) факт
Public power_CS_teor_calc_W As Double   ' мощность на входе в станцию управления  расчетная без корретировки
Public eff_d As Double              ' КПД системы в целом
' параметры описывающие кабельную систему
Public cable_type_num As Integer    ' номер кабеля todo сделать базу кабелей
Public cable_name As String         ' название кабеля
Public cable_R_Omkm As Double       ' удельное сопротивление
Public cable_Tmax_C As Double       ' температурный рейтинг  todo сделать проверки на температурный рейтинг
Public cable_dU_V As Double         ' падение напряжения на кабеле
Public cable_dPower_W As Double     ' мощность потребляемая кабелем
Public cable_power_W As Double      ' мощность на входе в кабель
Public freq_Hz_ As Double          ' частота питающего напряжения, Гц
Private slip_fr_ As Double            ' номинальная величина проскальзывания
                                    ' в отличии от насоса где частота соответствует частоте вращения вала
                                    ' в системе УЭЦН это частота тока и во вращение транслируется с проскальзыванием
Public cos_phi As Double            ' коэффициент мощности
Public I_A As Double                ' ток рабочий
Public load_fr As Double            ' загрузка ПЭД УЭЦН
                                    ' отношение фактической потребляемой мощности при работе
                                    ' к номинальной скорректированной на частоту
' параметры установки УЭЦН в скважине
'Public h_mes_top_m As Double
Private head_nom_m_ As Double
'Private ESP_length_m_ As Double
Public d_cas_mm As Double
'Public d_intake_mm As Double
Public angle_deg As Double
Private PKV_work_min_ As Double     ' время работы в ПКВ режиме
Private PKV_stop_min_ As Double     ' время ожидания в ПКВ режиме
' флюиды которые работают в системе УЭЦН
Private fluid_ As CPVT    ' флюид перед входом в насос
Public fluid_tub As CPVT    ' флюид в НКТ выше насоса
Public fluid_cas As CPVT    ' флюид в затрубном пространстве
Public fluidGasSep As CPVT ' флюид в газосепараторе
' параметры определяющие режим периодической кратковременной работы скважины
' ПКВ режим влияет на расчет показателей УЭЦН
'   - учитывается, что в период работы дебит равен мгновенному
'   - в период остановки дебита в системе нет
'
' класс ESPSystemSimple работает по следующей схеме
'   -  задает ЭЦН
'   -  задает флюид который притекает в зону приема УЭЦН
'   -  определяет сепарацию и распределяет флюиды по элементам системы
'         - что в насос
'         - что в НКТ (может отличаться от насоса для ПКВ режима)
'         - что в затруб
'   - задает электрические показатели системы УЭЦН и проводит расчет системы
'   - тепературный режим регулируется параметрами класса
'           t_int_C - температура на приеме насоса
'           t_dis_C - температура на выкиде насоса. если =-1 то рассчитывается при calc_along_flow = True
Public Property Get h_mes_top_m() As Double
    h_mes_top_m = ESP_pump_.h_mes_top_m
End Property
Public Property Let h_mes_top_m(val As Double)
    ESP_pump_.h_mes_top_m = val
End Property
Public Property Get h_mes_bottom_m() As Double
    h_mes_bottom_m = h_mes_top_m + ESP_pump_.length_m + ESP_motor.length_m
End Property
Public Property Get h_mes_intake_m() As Double
    h_mes_intake_m = h_mes_top_m + ESP_pump_.length_m
End Property
Public Property Get head_nom_m() As Double
    head_nom_m = head_nom_m_
End Property
Public Property Get freq_Hz() As Double
   freq_Hz = freq_Hz_
End Property
Public Property Let freq_Hz(val As Double)
   freq_Hz_ = val
   ESP_pump_.freq_Hz = val * (1 - slip_fr_)
End Property
Public Property Get ESP_pump() As CESPpump
   Set ESP_pump = ESP_pump_
End Property
Public Property Set ESP_pump(pmp As CESPpump)
   Set ESP_pump_ = pmp
   freq_nom_Hz = pmp.db.freq_Hz
   slip_fr_ = 1 - pmp.db.slip_nom_rpm / (pmp.db.freq_Hz * 60)
End Property
Public Property Get name() As String
    name = ESP_pump_.db.name + " " + ESP_pump_.db.manufacturer
End Property
Public Property Get power_CS_fact_W() As Double
   power_CS_fact_W = power_CS_fact_W_
End Property
Public Property Get energy_fact_Whday() As Double
   energy_fact_Whday = energy_fact_Whday_
End Property
Public Property Let energy_fact_Whday(val As Double)
   energy_fact_Whday_ = val
   power_CS_fact_W_ = val / 24 / PKV_frac
End Property
Public Property Get power_motor_W() As Double
   power_motor_W = power_motor_W_
End Property
Public Property Get U_trans_high_V() As Double
' напряжение на трансфоратере по высокой стороне
    U_trans_high_V = U_trans_high_V_
End Property
Public Property Get U_trans_low_V() As Double
' напряжение на трансфоратере по низкой стороне
    U_trans_low_V = U_trans_low_V_
End Property
Public Property Get trans_coef_fr() As Double
' коэффициент трансформации трансформатора
    trans_coef_fr = U_trans_high_V / U_trans_low_V
End Property
Public Property Get is_PKV() As Boolean
' оценка работает ли скважина в ПКВ режиме
    is_PKV = PKV_work_min_ > 0 And PKV_stop_min_ > 0
End Property
Public Property Let is_PKV(val As Boolean)
' нужна для отключения ПКВ режима
    If val Then
        Call set_PKV(30, 30)
    Else
        Call set_PKV(-1, -1)
    End If
End Property
Public Property Get PKV_work_min() As Double
    PKV_work_min = PKV_work_min_
End Property
Public Property Get PKV_stop_min() As Double
    PKV_stop_min = PKV_stop_min_
End Property
Public Sub set_PKV(ByVal PKV_work_min As Double, _
                  ByVal PKV_stop_min As Double)
' устновка ПКВ режима
' в виде отдельной процедуры, так как надо при этом модифицировать флюид
    PKV_work_min_ = PKV_work_min
    PKV_stop_min_ = PKV_stop_min
    If is_PKV Then
        Set fluid = fluid
    End If
End Sub
Private Sub PKV_update()
    ' обновляем все параметры чтобы лучше работать с ПКВ
    
    ' корректируем дебит
    ESP_pump_.fluid.qliq_sm3day = ESP_pump_.fluid.qliq_sm3day / PKV_frac
    power_CS_fact_W_ = energy_fact_Whday / 24 / PKV_frac
End Sub
' свойства для управления флюидом
Public Property Get fluid() As CPVT
    Set fluid = fluid_
End Property
Public Property Set fluid(val As CPVT)
    Set fluid_ = val
    ' сбросим также все другие флюиды
    Set fluid_tub = val
    Set fluid_cas = val
    Set fluidGasSep = val
    ' в насосе всегда свой флюид
    Set ESP_pump_.fluid = val.clone()
    Call PKV_update
End Property
Public Property Get ksep_total_fr() As Double
' общая сепарация УЭЦН параметр только для чтения
' для задания вручную надо задать естественную сепарацию и сепарацию газосепаратора
    ksep_total_fr = ksep_nat_fr + (1 - ksep_nat_fr) * ksep_gassep_fr
End Property
Public Property Get d_intake_mm() As Double
    d_intake_mm = ESP_pump.db.d_od_m
End Property
  
Private Sub Class_Initialize()
    ' парметры установки по умолчанию
    d_cas_mm = 150
  '  d_intake_mm = 100
    angle_deg = 90
 
    
    U_V = 1000
    U_trans_low_V_ = 380
    
    power_motor_nom_W = 30000
    freq_nom_Hz = 50
    motor_eff_fr = 0.9
    CS_eff_fr = 0.97
    Transform_eff_fr = 0.97
    Call set_cable(1)
    cable_dU_V = -1
    freq_Hz_ = 50
    slip_fr_ = 0.03
    cos_phi = 0.9
    
  '  dPower_protector_W = 400
    dPower_GasSep_W = 500
    
    isGasSep = True
    isManualSeparationCalc = byCorrealation
    ksep_nat_fr = 0.5
    ksep_gassep_fr = 0.5
    p_ksep_atma = 100
    t_ksep_C = 60
    
    PKV_work_min_ = 0
    PKV_stop_min_ = 0
    
End Sub
Public Sub init_dictionary(dict As Dictionary)
    Dim ESP_ID As String
    'Dim HeadNom_m As Double
    
    With dict
        ESP_ID = .Item("ESP_ID")
        Set ESP_pump = getESP(ESP_ID)
        If .Exists("HeadNom_m") Then
            head_nom_m_ = .Item("HeadNom_m")
            Call ESP_pump.set_num_stages(head_nom_m_)
        End If
        
        
        If .Exists("ESP_freq_Hz") Then freq_Hz = .Item("ESP_freq_Hz")             ' slip will be accounted
        If .Exists("ESP_U_V") Then U_V = .Item("ESP_U_V")
        If .Exists("ESP_cos_phi") Then cos_phi = .Item("ESP_cos_phi")
        If .Exists("MotorPowerNom_kW") Then power_motor_nom_W = .Item("MotorPowerNom_kW") * 1000
        If .Exists("ESP_energy_fact_kWhday") Then energy_fact_Whday = .Item("ESP_energy_fact_kWhday") * 1000
        If .Exists("ESP_cable_type") Then Call set_cable(.Item("ESP_cable_type"))
        
        If .Exists("t_intake_C") Then t_int_C = .Item("t_intake_C")
        If .Exists("t_dis_C") Then t_dis_C = .Item("t_dis_C")
        
        If .Exists("Ksep_GS_fr") Then ksep_gassep_fr = .Item("Ksep_GS_fr")
        If .Exists("Ksep_manual_fr") Then ksep_manual_fr = .Item("Ksep_manual_fr")
        
        If .Exists("ESP_h_mes_m") Then h_mes_top_m = .Item("ESP_h_mes_m")
        
        If .Exists("ESP_gas_correct") Then ESP_pump.gas_correct = .Item("ESP_gas_correct")
        If .Exists("c_calibr_head") Then ESP_pump.c_calibr_head = .Item("c_calibr_head")
        If .Exists("c_calibr_rate") Then ESP_pump.c_calibr_rate = .Item("c_calibr_rate")
        If .Exists("c_calibr_power") Then ESP_pump.c_calibr_power = .Item("c_calibr_power")
        If .Exists("d_st_int") Then ESP_pump.dnum_stages_integrate = .Item("d_st_int")
        
        If .Exists("PKV_work_min") And .Exists("PKV_stop_min") Then
            Call set_PKV(.Item("PKV_work_min"), .Item("PKV_stop_min"))
        End If
        
    End With
End Sub
Public Sub set_cable(ByVal cable_type As Integer)
    Select Case cable_type
        Case 1
            ' http://pskovgeokabel.ru/products/1_16/
            cable_R_Omkm = 1.18
            cable_name = "КПпАпБП-120 3x16"
            cable_Tmax_C = 120
    End Select
    ' здесь добавить другие марки кабеля при необходимости
    ' потом когда нибудь можно будет и расчет параметров кабеля сделать в термовставку тоже
    cable_type_num = cable_type
End Sub
Public Function calc_separation(p_atma As Double, t_C As Double)
' расчет сепарации и соответствующих параметров флюидов
' должен вызываться незавимо от расчета УЭЦН, так как при расчете сверху вниз
'   необходимо заранее рассчитать параметры флюида выше насоса, чтобы правильно посчитать НКТ
' p_atma  - давление при которой идет сепарация
' T_C - температура при которой идет сепарация
    Select Case isManualSeparationCalc
        Case byCorrealation
            ' все по сепарации по корреляциям
            p_ksep_atma = p_atma
            t_ksep_C = t_C
            Call fluid_.calc_PVT(p_ksep_atma, t_ksep_C)
            With fluid_
                ksep_nat_fr = unf_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, _
                                            .qliq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, _
                                            .sigma_oil_gas_Nm, .sigma_wat_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
                                    
            End With
        Case pressureManual
            ' берем значение ручное, давления как есть
             Call fluid_.calc_PVT(p_ksep_atma, t_ksep_C)
             With fluid_
                ksep_nat_fr = unf_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, _
                                            .qliq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, _
                                            .sigma_oil_gas_Nm, .sigma_wat_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
                                    
             End With
        Case valueManual
            ' берем и коэффициенты и давления как было задано при установке значений
            p_ksep_atma = p_atma
            t_ksep_C = t_C
            Call fluid_.calc_PVT(p_ksep_atma, t_ksep_C)
        Case fullyManual
            ' берем и коэффициенты и давления как было задано при установке значений
            Call fluid_.calc_PVT(p_ksep_atma, t_ksep_C)
        
    End Select
    
    ' потом, когда будет расчет газосепаратора - то надо будет разделить алгоритмы
    ' ручного задания и задания с использованием газосепаратора
    If isGasSep Then
        ksep_gassep_fr = ksep_gassep_fr
    Else
        ksep_gassep_fr = 0
    End If
    
    Set fluid_tub = fluid_.clone()
    Call fluid_tub.mod_after_separation(p_ksep_atma, t_ksep_C, ksep_total_fr, True)
    Set fluid_cas = fluid_.clone() ' тут надо будет доделать чтобы считать затруб с газом
    fluid_cas.q_gas_free_sm3day = fluid.q_gas_insitu_sm3day * ksep_total_fr
    Set fluidGasSep = fluid_.clone()
    Call fluidGasSep.mod_after_separation(p_ksep_atma, t_ksep_C, ksep_gassep_fr, True)
    ' в насосе свой флюид отличный от НКТ
    Set ESP_pump_.fluid = fluid_tub.clone()
    Call PKV_update
End Function
Public Property Get PKV_frac() As Double
    If is_PKV Then
        PKV_frac = PKV_work_min_ / (PKV_work_min_ + PKV_stop_min_)
    Else
        PKV_frac = 1
    End If
End Property
Public Function calc_ESPsys(p_atma As Double, _
                            Optional ByVal calc_along_flow As Boolean = True, _
                            Optional ByVal calc_temperature = False, _
                            Optional saveCurve As Boolean = False) As PTtype
' функция расчета параметров работы УЭЦН
'
' p_atma - давление начала расчета (давление на приеме если calc_along_flow = True)
' calc_along_flow - флаг показывает ведется ли расчет от давления на приеме или от выкида
'                  при расчете сверху вниз температура должна быть задана с двух сторон
'                  и сепарация будет учитываться в соответствии с параметрами заданными вручную
' calc_cfPower - флаг режима расчета корректировочного фактора для потребления энергии
' calc_temperature - флаг режима расчета перепада температуры в ЭЦН
    
    ' подготовим режима расчета температуры для ЭЦН
    If calc_temperature Then
        t_dis_C = -1
    ElseIf t_dis_C < 0 Then
        t_dis_C = t_int_C
    End If
    
        U_V = U_V
        
              slip_fr_ = ESP_motor.s_d
              ESP_pump_.freq_Hz = freq_Hz * (1 - slip_fr_)
            
              '1. расчет распределения давления в ЭЦН при работе УЭЦН
              Call ESP_pump_.calc_ESP(p_atma, t_int_C, t_dis_C, calc_along_flow, saveCurve)
              p_dis_atma = ESP_pump_.p_dis_atma
              p_int_atma = ESP_pump_.p_int_atma
              
              '2. Оценим мощность на валу
              power_shaft_W = ESP_pump_.power_ESP_W + ESP_motor.dPower_protector_W + dPower_GasSep_W
              
              Call ESP_motor.calc_motor_mom_Nm(power_shaft_W, freq_Hz, U_V)
              '3. Оценим электрическую мощность потребляемую ПЭД
              power_motor_W_ = ESP_motor.Pel_kW  ' power_shaft_W / motor_eff_fr
        
        '4. оценим потери в кабеле - для этого найдем примерно ток двигателя
        I_A = ESP_motor.I_lin_A ' power_motor_W_ / 1.732050808 / U_V / cos_phi
        
        '5 по данным тока и сопротивления кабеля найдем падение напряжения в кабеле
        cable_dU_V = I_A * cable_R_Omkm * h_mes_top_m / 1000
        U_trans_high_V_ = U_V + cable_dU_V
    '6 по падению напряжения оценим мощность рассеиваемую кабелем в тепло. учитываем 3 жилы. Тут учитывается полный ток и активный и реактивный
    cable_dPower_W = 3 * cable_dU_V * I_A
    '7 найдет мощность электрическую на входе в кабель
    cable_power_W = power_motor_W_ + cable_dPower_W
    '8. оценим потери мощность в трансформаторе и станции управления
    dPower_transform_W = (1 - CS_eff_fr) * cable_power_W
    dPower_CS_W = (1 - Transform_eff_fr) * (cable_power_W + dPower_transform_W)
    '9. оценим общую потребляемую мощность УЭЦН
    power_CS_teor_calc_W = cable_power_W + dPower_transform_W + dPower_CS_W
    If power_CS_teor_calc_W > 0 And power_CS_fact_W_ > 0 Then
        ESP_pump_.c_calibr_power = power_CS_fact_W_ / power_CS_teor_calc_W
    End If
    '10. внесем в мощность поправку на деградацию по мощности
    power_CS_calc_W = power_CS_teor_calc_W * c_calibr_power
    ' 11 рассчитаем загрузку двигателя, для этого получим номинальную мощность на данной частоте
    Dim PowNom_corr_W As Double
    PowNom_corr_W = power_motor_nom_W * freq_Hz / freq_nom_Hz   ' потом надо проверить что именно так считается номинальная мощность
    load_fr = power_motor_W_ / PowNom_corr_W
    ' 12 рассчитаем общий КПД системы по данным энергопотребления
    If power_CS_calc_W > 0 Then
        eff_d = ESP_pump_.power_fluid_W / power_CS_calc_W
    Else
        eff_d = 0
    End If
    calc_ESPsys = set_PT(ESP_pump_.p_dis_atma, ESP_pump_.t_dis_C)
End Function
 Public Sub set_ksep_total(calc_method As SEPAR_CALC_METHOD, ksep_nat_fr As Double, _
                         Optional Ksep_GS_fr As Double = 0, _
                         Optional p_ksep_atma As Double = -1, _
                         Optional t_ksep_C As Double = -1)
 ' устанавливает значение сепарации для уэцн вручную
 ' сначала установим значение газосепаратора - оно всегда одинаково для всех вариантов
    
    If Ksep_GS_fr > 0 And Ksep_GS_fr <= 1 Then
        isGasSep = True
    Else
       isGasSep = False
    End If
    ksep_gassep_fr = Ksep_GS_fr
    
    Me.p_ksep_atma = p_ksep_atma
    Me.t_ksep_C = t_ksep_C
    Me.ksep_nat_fr = ksep_nat_fr
    
    ' установим метод расчета
    ' но так чтобы он не противоречил введенным данным
     If ksep_nat_fr >= 0 And ksep_nat_fr <= 1 And p_ksep_atma > 0 And t_ksep_C > 0 Then
        ' если ввели хорошие параметры то можно применить любой метод расчета
         isManualSeparationCalc = calc_method
     ElseIf ksep_nat_fr >= 0 And ksep_nat_fr <= 1 And p_ksep_atma <= 0 Then
        ' плохое значение давления не позволяет полностью ручной метод
        If calc_method <> fullyManual Then
            isManualSeparationCalc = calc_method
        Else
            isManualSeparationCalc = valueManual
        End If
     ElseIf (ksep_nat_fr < 0 Or ksep_nat_fr > 1) And (p_ksep_atma > 0 And t_ksep_C > 0) Then
        ' плохая сепарация, но хорошие давления
         If calc_method = byCorrealation Or calc_method = pressureManual Then
            isManualSeparationCalc = calc_method
         Else
            isManualSeparationCalc = pressureManual
        End If
     Else
         ' плохая сепарация не позволяет ручной расчет
         isManualSeparationCalc = byCorrealation
     End If
     
 End Sub
 ' дебит газожидкостной смеси перед насосом в рабочих условиях
 Public Property Get q_mix_BeforePump_m3day() As Double
    With ESP_pump_
        fluid.calc_PVT .p_int_atma, .t_int_C
        q_mix_BeforePump_m3day = fluid.q_mix_rc_m3day
    End With
 End Property
 
 ' дебит газожидкостной смеси внути газосепаратора (после естественной сепарации)
 Public Property Get q_mix_InGasSep_m3day() As Double
    With ESP_pump_
        ' надо рассчитать свойства флюида после частичной сепарации
        fluidGasSep.calc_PVT .p_int_atma, .t_int_C
        q_mix_InGasSep_m3day = fluidGasSep.q_mix_rc_m3day
    End With
 End Property
 ' дебит газожидкостной смеси в насосе на первых ступенях (после естественной сепарации и газосепаратора)
 Public Property Get q_mix_PumpIntake_m3day() As Double
    With ESP_pump_
        fluid_tub.calc_PVT .p_int_atma, .t_int_C
        q_mix_PumpIntake_m3day = fluid_tub.q_mix_rc_m3day
    End With
 End Property
 
 ' дебит газожидкостной смеси на выкиде насоса (на последних ступенях)
 Public Property Get q_mix_PumpOutlet_m3day() As Double
    With ESP_pump_
        fluid_tub.calc_PVT .p_dis_atma, .t_dis_C
        q_mix_PumpOutlet_m3day = fluid_tub.q_mix_rc_m3day
    End With
 End Property
 
 Public Property Get gas_fraction_BeforePump_d() As Double
    With ESP_pump_
        fluid.calc_PVT .p_int_atma, .t_int_C
        gas_fraction_BeforePump_d = fluid.gas_fraction_d(0)
    End With
 End Property
 
 Public Property Get gas_fraction_InGasSep_d() As Double
    With ESP_pump_
        fluidGasSep.calc_PVT .p_int_atma, .t_int_C
        gas_fraction_InGasSep_d = fluidGasSep.gas_fraction_d(0)
    End With
 End Property
 Public Property Get gas_fraction_PumpIntake_d() As Double
    With ESP_pump_
        fluid_tub.calc_PVT .p_int_atma, .t_int_C
        gas_fraction_PumpIntake_d = fluid_tub.gas_fraction_d(0)
    End With
 End Property
 
 Public Property Get gas_fraction_PumpOutlet_d() As Double
    With ESP_pump_
        fluid_tub.calc_PVT .p_dis_atma, .t_dis_C
        gas_fraction_PumpOutlet_d = fluid_tub.gas_fraction_d(0)
    End With
 End Property
 
 Public Property Get c_calibr_head() As Double
    c_calibr_head = ESP_pump_.c_calibr_head
 End Property
 
 Public Property Let c_calibr_head(val As Double)
    ESP_pump_.c_calibr_head = val
 End Property
 
 Public Property Get c_calibr_power() As Double
    c_calibr_power = ESP_pump_.c_calibr_power
 End Property
  
 Public Property Get c_calibr_rate() As Double
    c_calibr_rate = ESP_pump_.c_calibr_rate
 End Property
 
Public Function array_out(Optional ByVal num_points As Integer = 20)
' подготовка массива для вывода в Excel
' num_points - количество точек в выходных массивах для вывода
'
    Dim arr()
    Dim M As Integer
   ' Dim FlowParams_out As PIPE_FLOW_PARAMS
    Dim offset As Integer
    Dim I As Integer
    Dim hh As Double
    Dim nrows As Integer
    
    offset = 2
On Error Resume Next
    arr = ESP_pump_.array_out(num_points)
    nrows = UBound(arr, 1)
    
    offset = nrows - 3
    
    ReDim Preserve arr(nrows, 22)
    I = 7
    arr(0, 0) = p_dis_atma - p_int_atma
    arr(1, 0) = "dP_atm"
    
   ' i = i + 1
   ' arr(0, i) = t_dis_C - t_int_C
   ' arr(1, i) = "dT_C"
    
   ' i = i + 1
   ' arr(0, i) = p_int_atma
   ' arr(1, i) = "p_int_atma"
    
   ' i = i + 1
   ' arr(0, i) = p_dis_atma
   ' arr(1, i) = "p_dis_atma"
    
    I = I + 1
    arr(0, I) = ESP_motor.length_m
    arr(1, I) = "length_motor_m"
    
    I = I + 1
    arr(0, I) = freq_Hz_
    arr(1, I) = "Pow freq_Hz"
    
    I = I + 1
    arr(0, I) = ESP_pump.freq_Hz
    arr(1, I) = "ESP freq_Hz"
    
    I = I + 1
    arr(0, I) = I_A
    arr(1, I) = "I, A"
    
    I = I + 1
    arr(0, I) = load_fr
    arr(1, I) = "load_fr"
    
    I = I + 1
    arr(0, I) = power_CS_calc_W / 1000
    arr(1, I) = "power_CS_kW"
    
    I = I + 1
    arr(0, I) = power_motor_W / 1000
    arr(1, I) = "power_motor_kW"
    
    I = I + 1
    arr(0, I) = power_shaft_W / 1000
    arr(1, I) = "power_shaft_kW"
     
    I = I + 1
    arr(0, I) = ESP_pump.power_ESP_W / 1000
    arr(1, I) = "power_ESP_kW"
        
    I = I + 1
    arr(0, I) = ESP_pump.power_fluid_W / 1000
    arr(1, I) = "power_fluid_kW"
        
    I = I + 1
    arr(0, I) = U_trans_high_V
    arr(1, I) = "U_trans_high_V"
    
    I = I + 1
    arr(0, I) = cable_dU_V
    arr(1, I) = "cable_dU_V"
         
    I = I + 1
    arr(0, I) = eff_d
    arr(1, I) = "eff_total_d"
    
    I = I + 1
    arr(0, I) = ESP_pump.eff_ESP_d
    arr(1, I) = "eff_ESP_d"
    
    I = I + 1
    arr(0, I) = c_calibr_power
    arr(1, I) = "c_calibr_power"
    
    I = 12
    I = I + 1
    arr(offset, I) = "Pmotor_nom_kW"
    arr(offset + 1, I) = power_motor_nom_W
    
    I = I + 1
    arr(offset, I) = "power_CS_fact_kW"
    arr(offset + 1, I) = power_CS_fact_W_ / 1000
    
    I = I + 1
    arr(offset, I) = "motor_eff_fr"
    arr(offset + 1, I) = motor_eff_fr
    
    I = I + 1
    arr(offset, I) = "cable_R_Omkm"
    arr(offset + 1, I) = cable_R_Omkm
    
    I = I + 1
    arr(offset, I) = "dPower_protector_kW"
    arr(offset + 1, I) = ESP_motor.dPower_protector_W / 1000
    
    I = I + 1
    arr(offset, I) = "dPower_GasSep_kW"
    arr(offset + 1, I) = dPower_GasSep_W / 1000
    
    I = I + 1
    arr(offset, I) = "dPower_CS_kW"
    arr(offset + 1, I) = dPower_CS_W / 1000
    
    I = I + 1
    arr(offset, I) = "dPower_transform_kW"
    arr(offset + 1, I) = dPower_transform_W / 1000
    
    I = I + 1
    arr(offset, I) = "cos_phi"
    arr(offset + 1, I) = cos_phi
    
    array_out = arr
End Function

