'=======================================================================================
'Unifloc 7.10  Apodemus agrarius                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
Option Explicit
'  класс для описания характеристик роторного газосепаратора УЭЦН
'  класс работает с расчетом сепарации вообще - при необходимости описывает входной модуль
'
' геометрические параметры насоса
Private length_m_ As Double                ' длина должна быть не нулевая, для того чтобы корректно строились графики по скважине
Private h_mes_top_ As Double                 ' глубина установки газосепаратора по верхней части
Private d_od_mm_ As Double                   ' внешний диаметр
Private angle_deg_ As Double               ' угол установки УЭЦН (предполагается, что по глубине угол не меняется)
Private d_cas_m_ As Double                  ' диаметр эксплуатационной колонны в месте установки сепаратора
Private q_nom_m3day_                        ' номинальный дебит для газосепаратора
Private p_curve_ As New CInterpolation      ' кривая распределения давления вдоль насоса   (как снаружи, так и внутри)
Private t_curve_ As New CInterpolation      ' кривая распределения температуры флюида вдоль насоса
' общие параметры
Private fluid_ As CPVT                     ' флюид исходный
Private fluid_tub_ As CPVT                  ' флюид после сепарации
Private fluid_cas_ As CPVT                  ' флюид после сепарации - то что идет в затруб
Private is_ksep_manual_ As Boolean           ' флаг для ручного учета газосепаратора
 ' параметры сепарации, расчетные значения
Private ksep_natural_d_ As Double
Private ksep_gassep_d_ As Double
Private ksep_total_d_  As Double
 
Private ksep_gassep_max_d_  As Double
Private q_mix_gassep_rc_m3day_ As Double
Private m_Nm_ As Double                    ' момент потребляемый
Private p_shaft_w_ As Double                ' мощность потребляемая
Private p_shaft_nom_w_ As Double             ' номинальная мощность потребляемая газосепаратором
Private w_rpm_ As Double                   ' скорость вращения вала
Private f_nom_Hz_ As Double                 ' номинальная скорость вращения газосепаратора
 
' свойство для выдачи момента
Public Property Get m_Nm() As Double
   m_Nm = m_Nm_
   m_Nm = p_shaft_w_ / w_radsec
End Property
Public Property Get w_radsec() As Double
   w_radsec = w_rpm_ / 60 * 2 * const_Pi
End Property
Public Property Get w_rpm() As Double
   w_rpm = w_rpm_
End Property
' свойство выдает сумарную кривую по давлениям
Public Property Get p_curve() As CInterpolation
   Set p_curve = p_curve_
End Property
Public Property Get t_curve() As CInterpolation
    Set t_curve = t_curve_
End Property
Public Property Get d_cas_m() As Double
    d_cas_m = d_cas_m_
End Property
'Public Property get Dintake_m() As Double
'    Dintake_m = p_Dintake_m
'End Property
Private Sub Class_Initialize()
    Call init_gassep(OD_mm:=125, _
                Qnom_m3day:=250, _
                h_mes_top_m:=0, _
                d_cas_mm:=0, _
                length_m:=3, _
                angle_deg:=90)
    is_ksep_manual_ = False
    
End Sub
Public Sub init_gassep(Optional OD_mm As Double = 125, _
                  Optional Qnom_m3day As Double = 250, _
                  Optional h_mes_top_m As Double = 0, _
                  Optional angle_deg As Double = 90, _
                  Optional d_cas_mm As Double = 0, _
                  Optional length_m As Double = 1)
' метод инициализации параметров газосепаратора
' в зависимости от габарита
' OD_mm     - определяет габарит
' h_mes_top_m - глубина подвеса в скважине, может пригодится когда то, пока про запас
' Angle_deg - угол установки к горизонтали 90 - вертикально
' d_cas_mm   - диаметр эксплуатационной колоны в месте установки
    d_od_mm_ = OD_mm
    angle_deg_ = angle_deg
    If d_cas_mm = 0 Then
        d_cas_m_ = OD_mm + 10
    Else
        d_cas_m_ = d_cas_mm
    End If
    h_mes_top_ = h_mes_top_m
    length_m_ = length_m
    q_nom_m3day_ = Qnom_m3day
    
    ' пока задаем тут - надо будет в зависимости от модели изменить потом
    p_shaft_nom_w_ = 1000            ' по умолчанию считаем что 1 квт уходит на сепарацию при номинальной частоте
    w_rpm_ = 3000                  ' по умолчанию 3000 оборотов в секунду
    f_nom_Hz_ = 50
    ksep_gassep_max_d_ = 0.9
    
End Sub
' =======================  геометрия
Public Property Get length_m() As Double
    length_m = length_m_
End Property
' глубина установки  (верхняя точка)
Public Property Get h_mes_top_m() As Double
   h_mes_top_m = h_mes_top_
End Property
'Public Property Let h_mes_top_m(val As Double)
'   h_mes_top_ = val
'End Property
' глубина нижней точки установки
Public Property Get h_mes_down_m() As Double
   h_mes_down_m = h_mes_top_m + length_m
End Property
Public Property Get d_od_m() As Double
   d_od_m = d_od_mm_ / 1000
End Property
' угол к горизонтали под которым установлена установка
' угол задается извне системой скважиной при установке системы в скважину
Public Property Get angle_deg() As Double
   angle_deg = angle_deg_
End Property
Public Property Get angle_vert_deg() As Double
   angle_vert_deg = angle_vert_deg - 90
End Property
Public Property Get fluid() As CPVT
   Set fluid = fluid_
End Property
Public Property Get fluid_tub() As CPVT
   Set fluid_tub = fluid_tub_
End Property
Public Property Get fluid_cas() As CPVT
   Set fluid_cas = fluid_cas_
End Property
Public Property Set fluid(val As CPVT)
  Set fluid_ = val       ' берем новый объект в работу
  Set fluid_tub_ = val
  Set fluid_cas_ = val
End Property
Public Property Get is_ksep_manual() As Boolean
   is_ksep_manual = is_ksep_manual_
End Property
Public Property Let is_ksep_manual(val As Boolean)
   is_ksep_manual_ = val
End Property
Public Property Get ksep_fr() As Double
   ksep_fr = ksep_total_d_
End Property
Public Property Let ksep_fr(val As Double)
   ksep_total_d_ = val
End Property
Private Sub Separatefluids()
' метод для принудительного разделения свойст флюида
   ' создаем новые экзампляры
   Set fluid_tub_ = New CPVT
   Set fluid_cas_ = New CPVT
   '  копируем в них значения
   fluid_tub_.Copy fluid_
   fluid_cas_.Copy fluid_
End Sub
Public Sub calc_gassep(pt As PTtype, Optional f_Hz As Double = 50)
' метод расчета параметров газосепаратора при заданных термобарических условиях
' считает, сепарацию, потребляемую мощность, при необходимости другие параметры
' расчитывается естественная и искуственная сепарация, модифицируются флюиды после сепарации
' возвращается значение коэффициента сепарации общего
'
    
    p_curve_.ClearPoints
    t_curve_.ClearPoints
    With fluid
        .calc_PVT_PT pt
        If Not is_ksep_manual Then
            ksep_natural_d_ = calc_NatSeparation_d()
            q_mix_gassep_rc_m3day_ = .q_liq_rc_m3day + .q_gas_rc_m3day * (1 - ksep_natural_d_)
            ksep_gassep_d_ = calc_GSsep_d(q_mix_gassep_rc_m3day_)
            ksep_total_d_ = ksep_natural_d_ + (1 - ksep_natural_d_) * ksep_gassep_d_
        End If
    End With
    If ksep_total_d_ > 0 Then
        ' разделяем и модифицируем свойства флюидов после сепарации
        Call Separatefluids
        Call fluid_tub_.mod_after_separation(pt.p_atma, pt.t_C, ksep_total_d_, GasGoesIntoSolution)
        fluid_cas_.ZNLF = True
    End If
    ' поскольку основная мощность потребляемая газосепаратором тратится шнеком на повышение давления
    ' то можно предположить, что потери мощность с ростом частоты будут расти в кубе
    ' при этом зависимости от дебита выраженной нет, поскольку будет меняться КПД
    ' это предположение требует уточнения (11.07.2018 rnt)
    p_shaft_w_ = p_shaft_nom_w_ * (f_Hz / f_nom_Hz_) ^ 3
    
    ' добавим точки для газосепаратора
    p_curve_.AddPoint h_mes_top_m, pt.p_atma
    p_curve_.AddPoint h_mes_down_m, pt.p_atma
    t_curve_.AddPoint h_mes_top_m, pt.t_C
    t_curve_.AddPoint h_mes_down_m, pt.t_C
    
End Sub
Private Function calc_GSsep_d(q_mix_rc_m3day As Double)
' расчет коэффициента сепарации газосепаратора
' предполагается линейная зависимость от дебита
' q_mix_rc_m3day - расход ГЖС поступающей на вход с учетом естественной сепарации с рабочих условиях
' потом хорошо бы уточнить и приспособить для разных видов конструкции
    Dim KS As Double
    Dim q_mix_ As Double
    Dim qmax As Double
    q_mix_ = q_mix_rc_m3day
    qmax = q_nom_m3day_
    If q_mix_ < qmax Then
        KS = (q_mix_ / qmax) * ksep_gassep_max_d_
    Else
        KS = 0
    End If
    calc_GSsep_d = KS
End Function
Private Function calc_NatSeparation_d() As Double
    With fluid
        calc_NatSeparation_d = unf_calc_natural_separation(d_od_mm_ / 1000, d_cas_m_, _
                                                        .q_liq_sm3day, .q_gas_insitu_sm3day, .bo_m3m3, .bg_m3m3, .sigma_oil_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
    End With
End Function
'' метод построения кривой зависимости коэффициента сепарации от дебита
'Public Function Build_SeparCurve()
'
''    Dim i As Integer
''    Dim qtest As Double, Rptest As Double
''    Dim qinit As Double, Rpinit As Double, Rsbinit As Double
''    Dim qmin As Double, Qmax As Double
''    Dim Ks As Double
''    Dim numPoint As Integer
''
''    curve(str_ksep_natQl_curve).ClearPoints
''    curve(str_ksep_gassepQl_curve).ClearPoints
''    curve(str_ksep_totalQl_curve).ClearPoints
''
''    curve(str_ksep_natRp_curve).ClearPoints
''    curve(str_ksep_gassepRp_curve).ClearPoints
''    curve(str_ksep_totalRp_curve).ClearPoints
''
''    numPoint = 20
''
''    qmin = 0: Qmax = 500
''
''    'одолжим ненадолго дебит по скважине
''    qinit = q_liq_sm3day
''    Rpinit = fluid.rp_m3m3
''    Rsbinit = fluid.rsb_m3m3
''
''    For i = 0 To numPoint
''        qtest = (Qmax - qmin) / numPoint * i + qmin
''        q_liq_sm3day = qtest
''        Ks = calc_SeparationAtIntake_d(p_int_atma_, t_int_C_)
''
''        curve(str_ksep_natQl_curve).AddPoint qtest, ksep_natural_d_
''        curve(str_ksep_gassepQl_curve).AddPoint qtest, ksep_gassep_d_
''        curve(str_ksep_totalQl_curve).AddPoint qtest, ksep_total_d_
''    Next i
''
''    ' вернем дебит по скважине назад
''    q_liq_sm3day = qinit
''
''    ' далее построим кривые по сепарации изменяя газовый фактор на скважине
''    Rptest = max(fluid.rsb_m3m3 - 100, 50)
''
''    For i = 0 To numPoint
''        Rptest = Rptest + 10
''        fluid.rp_m3m3 = Rptest
''
''        Ks = calc_SeparationAtIntake_d(p_int_atma_, t_int_C_)
''
''        curve(str_ksep_natRp_curve).AddPoint qtest, ksep_natural_d_
''        curve(str_ksep_gassepRp_curve).AddPoint qtest, ksep_gassep_d_
''        curve(str_ksep_totalRp_curve).AddPoint qtest, ksep_total_d_
''    Next i
''    fluid.rp_m3m3 = Rpinit
''    fluid.rsb_m3m3 = Rsbinit
'
'End Function
'
