'=======================================================================================
'Unifloc 7.8  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
' функции расчета скважины для проведения расчетов из интерфейса Excel
Option Explicit
' ==============  функции для расчета скважины ==========================
' =====================================================================
' функция для шаблонного чтения данных по скважине в интерфейсных функциях
Private Function well_InitData(q_m3day, _
                               fw_perc, _
                      Optional Pcas_atma As Double = 10, _
                      Optional wellStr As String = WELL_DEFAULT, _
                      Optional PVTstr As String = PVT_DEFAULT, _
                      Optional ESPstr As String = ESP_DEFAULT, _
                      Optional HydrCorr As H_CORRELATION = 0, _
                      Optional ksep_fr As Double = 0, _
                      Optional Kdegr_d As Double = 0) As CWellESP
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' ----------- опциональные параметры
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' wellStr  - закодированные параметры конструкции скважины
' PVTstr - закодированные параметры флюидов
' ESPStr - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d - коэффициент деградации УЭЦН
' на входе данные по конструкции скважины, PVT, ЭЦН
' берутся из строк с закодированными параметрами
' следует использовать функции Encode для передачи параметров
' на выходе объект скважина с загруженными данными
    
    Dim well As New CWellESP
    Dim PVT As New CPVT
    Dim pmp As CESPpump
    Dim ESPsystem As New CESPsystemSimple
    ' read PVT data from encoded string
    Set PVT = PVT_decode_string(PVTstr)
    ' set liquid rate
    PVT.qliq_sm3day = q_m3day
    ' set water cut - correcting percent to fraction
    PVT.fw_fr = fw_perc / 100
    ' set well construction from encoded string
    ' also set temperature parameters (BHT and WHT)
    Set well = well_decode_string(wellStr)
    ' link well with fluid object
    Set well.fluid = PVT
    ' set casing pressure
    well.Pcas_atma = Pcas_atma
    ' set hydraulic correlation
    well.HFlowCorrelation = HydrCorr
    ' set temperature model with temperature gradient
    well.TempCorrelation = GeoGradTemp
    ' read ESP object from encoded string
    If ESPstr = -1 Then
        Set well_InitData = well
    Else
        Set ESPsystem = ESP_decode_string(ESPstr)
        ' link well with ESP object
        Set well.ESP = ESPsystem
        ' set correction factor for ESP
        well.ESP.cfHead_fr = Kdegr_d
        ' set separation coefficient if given
        If ksep_fr > 0 And ksep_fr <= 1 Then
            Call well.SetKseptotal_d(valueManual, ksep_fr, 0)
        Else
            Call well.SetKseptotal_d(byCorrealation, ksep_fr)
        End If
    End If
    
    
    ' return result well object
    Set well_InitData = well
End Function
' function prepares output array for public functions
' подготовим данные для вывода
Private Function well_out_arr(well As CWellESP, Optional FirsrCol As Integer = 0)
' данные выводятся в одну линию, чтобы, хотя бы теоретически, можно было вывести данные по скважине в таблице
' во второй строке выводятся подписи параметров, если необходимо
' первый параметр настраиваемый
    Dim ar1, ar2
    Dim res As New Dictionary
    Const arr_len = 25
    ReDim ar1(arr_len)
    ReDim ar2(arr_len)
    Dim i As Integer
    With well
         res.Add "Pline_atma", .Pline_atma                                  'i=1
         res.Add "pbuf_atma", .pbuf_atma                                    'i=2
         res.Add "Pdis_atma", .Pdis_atma                                    'i=3
         res.Add "Pint_atma", .Pint_atma                                    'i=4
         res.Add "pwf_atma", .pwf_atma                                      'i=5
         res.Add "Pcas_atma", .Pcas_atma                                    'i=6
         res.Add "Hdyn_m", .Hdyn_m                                          'i=7
         res.Add "ESP.power_CS_calc_W", .ESP.power_CS_calc_W                'i=8
         res.Add "ESP.freq_Hz", .ESP.freq_Hz                                'i=9
         res.Add "ESP.I_A", .ESP.I_A                                        'i=10
         res.Add "ESP.load_fr", .ESP.load_fr                                'i=11
         res.Add "ESP.ESPpump.EffiencyESP_d", .ESP.ESPpump.EffiencyESP_d    'i=12
         res.Add "ESP.KsepTotal_fr", .ESP.KsepTotal_fr                      'i=13
         res.Add "ESP.KsepNat_fr", .ESP.KsepNat_fr                          'i=14
         res.Add "ESP.KSepGasSep_fr", .ESP.KSepGasSep_fr                    'i=15
         res.Add "Tsurf_C", .Tsurf_C                                        'i=16
         res.Add "twh_C", .twh_C                                            'i=17
         res.Add "Tdis_C", .Tdis_C                                          'i=18
         res.Add "Tintake_C", .Tintake_C                                          'i=19
         res.Add "tbh_C", .tbh_C                                            'i=20
         res.Add "ESP.cfHead_fr", .ESP.cfHead_fr                            'i=21
         res.Add "choke.cfChoke_fr", .choke.cfChoke_fr                      'i=22
         res.Add "qliq_sm3day", .qliq_sm3day                                'i=23
         res.Add "fw_perc", .fw_perc                                        'i=24
        
        For i = 1 To arr_len - 1
            ar1(i) = res.Items()(i - 1): ar2(i) = res.Keys()(i - 1)
        Next i
        ' first column can have any number
        ' set it here
        ar1(0) = ar1(FirsrCol): ar2(0) = ar2(FirsrCol)
                
        well_out_arr = Array(ar1, ar2)
    ' можно еще добавить сюда вывод кривых распределения давления и температуры по стволу и еще 4 параметров  (потом)
    End With
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета линейного давления по забойному для скважины
' расчет снизу-вверх, простой и быстрый расчет
Public Function well_plin_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal Pcas_atma As Double = -1, _
        Optional ByVal wellStr As String = WELL_DEFAULT, _
        Optional ByVal PVTstr As String = PVT_DEFAULT, _
        Optional ByVal ESPstr As String = ESP_DEFAULT, _
        Optional ByVal HydrCorr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = 0, _
        Optional ByVal Kdegr_d As Double = 0, _
        Optional ByVal param_num As Integer = 1 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
' ----------- опциональные параметры
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' wellStr  - закодированные параметры конструкции скважины
' PVTstr - закодированные параметры флюидов
' ESPStr - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_InitData(q_m3day, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr, ksep_fr, Kdegr_d)
    ' call calc method
    Call well.calc_plin_pwf_atma(pwf_atma)
    ' return results
    well_plin_pwf_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета давления на приеме по забойному для скважины
' расчет снизу-вверх, считает только участок ниже насоса
Public Function well_Pintake_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal Pcas_atma As Double = -1, _
        Optional ByVal wellStr As String = WELL_DEFAULT, _
        Optional ByVal PVTstr As String = PVT_DEFAULT, _
        Optional ByVal ESPstr As String = ESP_DEFAULT, _
        Optional ByVal HydrCorr As H_CORRELATION = 0 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
' ----------- опциональные параметры
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' wellStr  - закодированные параметры конструкции скважины
' PVTstr - закодированные параметры флюидов
' ESPStr - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_InitData(q_m3day, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr)
    ' return results
    well_Pintake_pwf_atma = well.calc_Pintake_pwf_atma(pwf_atma)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления по устьевому для скважины
' расчет сверху-вниз, считает быстро за счет угадывания сепарации
'                     температура только линейная или по градиенту
Public Function well_pwf_plin_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal plin_atma As Double, _
        Optional ByVal Pcas_atma As Double = -1, _
        Optional ByVal wellStr As String = WELL_DEFAULT, _
        Optional ByVal PVTstr As String = PVT_DEFAULT, _
        Optional ByVal ESPstr As String = ESP_DEFAULT, _
        Optional ByVal HydrCorr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = -1, _
        Optional ByVal Psep_atma As Double = 40, _
        Optional ByVal Tsep_C As Double = 40, _
        Optional ByVal Kdegr_d As Double = 0, _
        Optional ByVal param_num As Integer = 5, _
        Optional ByVal calc_self_flow_well = False)
' функция расчета забойного давления скважины по линейному
' на основе устьевых параметров работы скважины
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' plin_atma - линейное (устьевое) давление
' ----------- опциональные параметры
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' wellStr  - закодированные параметры конструкции скважины
' PVTstr - закодированные параметры флюидов
' ESPStr - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Psep_atma - давление сепарации
' Tsep_C - температура сепарации
'           при расчете сверху вниз неизвестны параметры сепарации
'           если задать их явно (угадать)
'           тогда расчет упрощается и ускоряется
' Kdegr_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    Dim well As CWellESP
    Dim ar
    If calc_self_flow_well = 1 Then
        ESPstr = -1
    End If
    
    ' read all data to well object
    Set well = well_InitData(q_m3day, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr, ksep_fr, Kdegr_d)
    ' при расчете сверху вниз задаем какие то значения сепарации наугад
    If calc_self_flow_well <> 1 Then
        Call well.SetKseptotal_d(fullyManual, ksep_fr, 0, Psep_atma, Tsep_C)
        well.isCalcCasing = True
    End If
    
    ' проведем расчет
    Call well.calc_pwf_plin_atma(plin_atma, well.tbh_C, fast:=True)
    If calc_self_flow_well = 1 Then
        well_pwf_plin_atma = well.pwf_atma
    Else
    ' в качестве результата выведем ряд расчитанных значений давления
        well_pwf_plin_atma = well_out_arr(well, param_num)
    End If
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция адаптации модели скважины по данным эксплуатации
' подбирает коэффициента деградации УЭЦН и штуцера
' по замера на поверхности и на забое/приеме насоса
Public Function well_calcKdegr_fr( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal Pdown_atma As Double, _
                     ByVal pbuf_atma As Double, _
            Optional Pdown_at_intake As Boolean = False, _
            Optional ByVal plin_atma As Double = -1, _
            Optional ByVal Pcas_atma As Double = -1, _
            Optional ByVal wellStr As String = WELL_DEFAULT, _
            Optional ByVal PVTstr As String = PVT_DEFAULT, _
            Optional ByVal ESPstr As String = ESP_DEFAULT, _
            Optional ByVal HydrCorr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = -1, _
            Optional ByVal Kdegr_d As Double = 0, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' Pdown_atma - давление ниже насоса (внизу) для расчета
'     либо забойное давление (по умолчанию)
'     либо давление на приеме
'     определяется опциональным параметром Pdown_at_intake
' pbuf_atma  - буферное давление
' ----------- опциональные параметры
' Pdown_at_intake - флаг определяет точку расчета давления
'                   ниже насоса. По умолчанию забойное
' plin_atma - линейное давление
'             если не задано штуцер не учитывается
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' wellStr  - закодированные параметры конструкции скважины
' PVTstr - закодированные параметры флюидов
' ESPStr - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    
    Dim well As CWellESP
    Dim pwf_temp_atma As Double
    Dim ar
On Error GoTo err1:
    ' read all data to well object
    Set well = well_InitData(q_m3day, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr, ksep_fr, Kdegr_d)
    ' calc BHP if needed from intake pressure
    If Pdown_at_intake Then
        pwf_temp_atma = well.calc_pwf_Pintake_atma(Pdown_atma)
    Else
        pwf_temp_atma = Pdown_atma
    End If
    ' проведем расчет
    If well.calc_well(plin_atma, pbuf_atma, pwf_temp_atma, Pcas_atma) Then
    ' в качестве результата выведем ряд расчитанных значений давления
        well_calcKdegr_fr = well_out_arr(well, param_num)
    Else
        well_calcKdegr_fr = "Kdegr not found. Check log for details"
    End If
    Exit Function
err1:
    Dim errmsg As String
    errmsg = "well_calcKdegr_fr: error" & sDELIM & Err.Description
    addLogMsg errmsg
    well_calcKdegr_fr = errmsg
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления скважины по динамическому уровню
Public Function well_pwf_Hdyn_atma( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal Pcas_atma As Double, _
                     ByVal Hdyn_m As Double, _
            Optional ByVal wellStr As String = WELL_DEFAULT, _
            Optional ByVal PVTstr As String = PVT_DEFAULT, _
            Optional ByVal ESPstr As String = ESP_DEFAULT, _
            Optional ByVal HydrCorr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = 0, _
            Optional ByVal Kdegr_d As Double = 0, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day   - дебит жидкости, на поверхности
' fw_perc   - обводненность (объемная на поверхности)
' Pcas_atma - затрубное давление
' Hdyn_m    - динамический уровень (при данном затрубном)
' ----------- опциональные параметры
' wellStr   - закодированные параметры конструкции скважины
' PVTstr    - закодированные параметры флюидов
' ESPStr    - закодированные параметры УЭЦН
' HydrCorr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    
    Dim well As CWellESP
    Set well = well_InitData(q_m3day, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr, ksep_fr, Kdegr_d)
    Call well.calc_pwf_PanHd_atma(Pcas_atma, Hdyn_m, well.tbh_C)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    Dim ar
    well_pwf_Hdyn_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета узлового анализа системы "пласт - скважина - УЭЦН"
' по заданным параетрам пласта, скважины и УЭЦН
' определяется рабочий дебит и забойное давление
Public Function nodal_qliq_sm3day( _
             ByVal pi_sm3dayatm As Double, _
             ByVal plin_atma As Double, _
             ByVal fw_perc As Double, _
    Optional ByVal Pres_atma = 250, _
    Optional ByVal Pcas_atma As Double = 10, _
    Optional ByVal wellStr As String = WELL_DEFAULT, _
    Optional ByVal PVTstr As String = PVT_DEFAULT, _
    Optional ByVal ESPstr As String = ESP_DEFAULT, _
    Optional ByVal HydrCorr As H_CORRELATION = 0, _
    Optional ByVal ksep_fr As Double = 0, _
    Optional ByVal Kdegr_d As Double = 0, _
    Optional ByVal param_num As Integer = 23)
' исходные параметры
' pi_sm3dayatm - коэффициент продуктивности пласта
' plin_atma - линейное давление
' fw_perc   - обводненность (объемная на поверхности)
' ----------- опциональные параметры
' Pres_atma - пластовое давление
' Pcas_atma - затрубное давление (для определения Ндин)
' wellStr   - закодированные параметры конструкции скважины
' PVTstr    - закодированные параметры флюидов
' ESPStr    - закодированные параметры УЭЦН
' HydrCorr  - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Kdegr_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы "пласт - скважина - УЭЦН"
'description_end
    Dim PS As New CProdSys
    Dim well As CWellESP
    Dim res As New CReservoirVogel
    Dim ar
    ' init all data
    Set well = well_InitData(0, fw_perc, Pcas_atma, _
                             wellStr, PVTstr, ESPstr, HydrCorr, ksep_fr, Kdegr_d)
    res.InitProp Pres_atma, well.fluid.pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    Set PS.well = well
    Set PS.Reservoir = res
    
    Call PS.nodal_qliq_sm3day(plin_atma)
    
    well.qliq_sm3day = PS.qsol_scm3day
    
    Call well.calc_plin_pwf_atma(PS.pwfsol_atma)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    
    nodal_qliq_sm3day = well_out_arr(well, param_num)
    
End Function
