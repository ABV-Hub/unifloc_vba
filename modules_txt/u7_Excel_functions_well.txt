'=======================================================================================
'Unifloc 7.12  Apodemus agrarius                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
' функции расчета скважины для проведения расчетов из интерфейса Excel
Option Explicit
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' Расчет устьевого давления скважины,
' расчет распределения давления и температуры в скважине
' с использованием многофазных корреляций.
Public Function well_plin_pwf_new_atma( _
                 ByVal qliq_sm3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
                 ByVal h_perf_m As Double, _
        Optional ByVal pcas_atma As Double, _
        Optional ByVal d_choke_mm As Double, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_AL As String, _
        Optional ByVal hmes_habs_list_m As Variant, _
        Optional ByVal dtub_list_mm As Variant, _
        Optional ByVal dcas_list_mm As Variant, _
        Optional ByVal temp_list_C As Variant, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal twf_C As Double, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal param_out As Integer = 1, _
        Optional ByVal num_pt_crv As Integer = 21)
' Обязательные параметры
' qliq_sm3day - дебит жидкости в поверхностных условиях
' fw_perc     - обводненность
' pwf_atma    - забойное давление с которого начинается расчет, атм
'               граничное значение для проведения расчета
' h_perf_m    - измеренная глубина пласта (перфорации)
'               точка узлового анализа при узле на забое скважины
' pcas_atma   - затрубное давление (расчета Ндин)
' d_choke_mm  - диаметр штуцера
' str_PVT     - закодированная строка с параметрами PVT.
'               если задана - перекрывает другие значения
' str_AL      - закодированная параметров мех добычи.
'               строка параметров ЭЦН либо строка параметров газлифта
' hmes_habs_list_m  - траектория скважины. range или таблица [0..N,0..1]
' dtub_list_mm   - диаметр НКТ. range или таблица [0..N,0..1]
' dcas_list_mm   - диаметр эксп колонны. range или таблица [0..N,0..1]
' temp_list_C    - температура среды. range или таблица [0..N,0..1]
' hydr_corr    - гидравлическая корреляция, H_CORRELATION
'                  BeggsBrill = 0
'                  Ansari = 1
'                  Unified = 2
'                  Gray = 3
'                  HagedornBrown = 4
'                  SakharovMokhov = 5
' temp_method  - температурная модель
' twf_C         - температура флюида на забое
'                 необходима для продвинутого учета температуры
' c_calibr_grav - поправка на гравитационную составляющую
'               перепада давления
' c_calibr_fric - поправка на трение в перепаде давления
' roughness_m   - шероховатость трубы
' q_gas_sm3day  - свободный газ поступающие в трубу.
' param_out     - номер параметра для вывода в ячейку [0,0]
' num_pt_crv    - число параметров вывода массивов
' результат   - число - давление на другом конце трубы atma.
'description_end
    
    Dim well ' As CWellESP
On Error GoTo err1:
            
    Set well = well_load_new( _
                    qliq_sm3day, _
                    fw_perc, _
                    h_perf_m, _
                    pcas_atma, _
                    d_choke_mm, _
                    str_PVT, _
                    str_AL, _
                    hmes_habs_list_m, _
                    dtub_list_mm, _
                    dcas_list_mm, _
                    temp_list_C, _
                    hydr_corr, _
                    temp_method, _
                    c_calibr_grav, _
                    c_calibr_fric, _
                    roughness_m, _
                    q_gas_sm3day)
    
    Dim res1
    res1 = well.calc_plin_pwf_atma(pwf_atma, twf_C, saveCurve:=allCurves)
    well_plin_pwf_new_atma = well_out_arr(well, param_out, num_pt_crv)
    'well_plin_pwf_new_atma = res1
    Exit Function
err1:
    well_plin_pwf_new_atma = Array(-1, "error")
    addLogMsg "Error:well_plin_pwf_new_atma:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' Расчет забойного давления скважины,
' расчет распределения давления и температуры в скважине
' с использованием многофазных корреляций
Public Function well_pwf_plin_new_atma( _
                 ByVal qliq_sm3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal plin_atma As Double, _
                 ByVal h_perf_m As Double, _
        Optional ByVal pcas_atma As Double, _
        Optional ByVal d_choke_mm As Double, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_AL As String, _
        Optional ByVal hmes_habs_list_m As Variant, _
        Optional ByVal dtub_list_mm As Variant, _
        Optional ByVal dcas_list_mm As Variant, _
        Optional ByVal temp_list_C As Variant, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal param_out As Integer = 1, _
        Optional ByVal num_pt_crv As Integer = 21)
' Обязательные параметры
' qliq_sm3day - дебит жидкости в поверхностных условиях
' fw_perc     - обводненность
' plin_atma   - линейное давление с которого начинается расчет, атм
'               граничное значение для проведения расчета
' h_perf_m    - измеренная глубина пласта (перфорации)
'               точка узлового анализа при узле на забое скважины
' pcas_atma   - затрубное давление (расчета Ндин)
' d_choke_mm  - диаметр штуцера
' str_PVT     - закодированная строка с параметрами PVT.
'               если задана - перекрывает другие значения
' str_AL      - закодированная параметров мех добычи.
'               строка параметров ЭЦН либо строка параметров газлифта
' hmes_habs_list_m  - траектория скважины. range или таблица [0..N,0..1]
' dtub_list_mm   - диаметр НКТ. range или таблица [0..N,0..1]
' dcas_list_mm   - диаметр эксп колонны. range или таблица [0..N,0..1]
' temp_list_C    - температура среды. range или таблица [0..N,0..1]
' hydr_corr    - гидравлическая корреляция, H_CORRELATION
'                  BeggsBrill = 0
'                  Ansari = 1
'                  Unified = 2
'                  Gray = 3
'                  HagedornBrown = 4
'                  SakharovMokhov = 5
' temp_method  - температурная модель
' c_calibr_grav - поправка на гравитационную составляющую
'               перепада давления
' c_calibr_fric - поправка на трение в перепаде давления
' roughness_m   - шероховатость трубы
' q_gas_sm3day  - свободный газ поступающие в трубу.
' param_out     - номер параметра для вывода в ячейку [0,0]
' num_pt_crv    - число параметров вывода массивов
' результат   - число - давление на другом конце трубы atma.
'description_end
    
    Dim well As CWellESP
On Error GoTo err1:
            
    Set well = well_load_new( _
                    qliq_sm3day, _
                    fw_perc, _
                    h_perf_m, _
                    pcas_atma, _
                    d_choke_mm, _
                    str_PVT, _
                    str_AL, _
                    hmes_habs_list_m, _
                    dtub_list_mm, _
                    dcas_list_mm, _
                    temp_list_C, _
                    hydr_corr, _
                    temp_method, _
                    c_calibr_grav, _
                    c_calibr_fric, _
                    roughness_m, _
                    q_gas_sm3day)
    
    Dim res1
    res1 = well.calc_pwf_plin_atma(plin_atma, 100, fast:=True, saveCurve:=allCurves)
    well_pwf_plin_new_atma = well_out_arr(well, param_out, num_pt_crv)
    Exit Function
err1:
    well_pwf_plin_new_atma = Array(-1, "error")
    addLogMsg "Error:well_pwf_plin_new_atma:" & Err.Description
End Function
Private Function well_load_new( _
                 ByVal qliq_sm3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal h_perf_m As Double, _
        Optional ByVal pcas_atma As Double, _
        Optional ByVal d_choke_mm As Double, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_AL As String, _
        Optional ByVal hmes_habs_list_m As Variant, _
        Optional ByVal dtub_list_mm As Variant, _
        Optional ByVal dcas_list_mm As Variant, _
        Optional ByVal temp_list_C As Variant, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0)
' Обязательные параметры
' qliq_sm3day - дебит жидкости в поверхностных условиях
' fw_perc     - обводненность
' pwf_atma    - забойное давление с которого начинается расчет, атм
'               граничное значение для проведения расчета
' h_perf_m    - измеренная глубина пласта (перфорации)
'               точка узлового анализа при узле на забое скважины
' pcas_atma
' d_choke_mm
' str_PVT      - закодированная строка с параметрами PVT.
'               если задана - перекрывает другие значения
' str_AL       - закодированная параметров мех добычи.
'            либо строка параметров ЭЦН либо строка параметров газлифта
' hmes_habs_list_m  - траектория скважины. range или таблица [0..N,0..1]
' dtub_list_mm
' dcas_list_mm
' temp_list_C
' hydr_corr    - гидравлическая корреляция, H_CORRELATION
'                  BeggsBrill = 0
'                  Ansari = 1
'                  Unified = 2
'                  Gray = 3
'                  HagedornBrown = 4
'                  SakharovMokhov = 5
' temp_method
' c_calibr_grav  - поправка на гравитационную составляющую
'               перепада давления
' c_calibr_fric  - поправка на трение в перепаде давления
' roughness_m - шероховатость трубы
' q_gas_sm3day - свободный газ поступающие в трубу.
' результат   - число - давление на другом конце трубы atma.
'description_end
    
    Dim PVT As New CPVT
    Dim tr_cas As New CPipeTrajectory
    Dim tr_tub As New CPipeTrajectory
    Dim amb As New CAmbientFormation
    Dim temp_crv As New CInterpolation
    Dim well 'As CWellESP
    Dim well_gl As CWellGL
    Dim ESPsystem As CESPsystemSimple
    Dim GLVset As CGLValveSet
    Dim well_type As Integer
On Error GoTo err1:
        
    Call tr_cas.init_from_vert_range(hmes_habs_list_m, dcas_list_mm)
    tr_cas.roughness_m = roughness_m
    Call tr_tub.init_from_vert_range(hmes_habs_list_m, dtub_list_mm)
    tr_tub.roughness_m = roughness_m
    
    ' определим тип скважины, чтобы создать соответствующий объект
    ' подробно прочитаем свойства позже
    If Len(str_AL) < 3 Then
        well_type = 0
    Else
        If Left(str_AL, 3) = "ESP" Then
            well_type = 1
        ElseIf Left(str_AL, 3) = "GLV" Then
            well_type = 2
        End If
    End If
    
    Set well = new_well_factory(h_perf_m, tr_cas:=tr_cas, tr_tub:=tr_tub, well_type:=well_type)
    Call amb.init_amb_temp(h_perf_m, tamb_arr_C:=temp_list_C)
    Set well.ambient_formation = amb
    
    Set PVT = PVT_decode_string(str_PVT) ' initialize PVT properties
    PVT.qliq_sm3day = qliq_sm3day ' set liquid rate and watercut
    PVT.fw_perc = fw_perc
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set well.fluid = PVT
    
    well.choke.d_choke_m = d_choke_mm
    
    Call well.set_param(calc_along_coord:=False, _
                        flow_along_coord:=False, _
                        hcor:=hydr_corr, _
                        temp_method:=temp_method)
    
    well.c_calibr_fric = c_calibr_fric
    well.c_calibr_grav = c_calibr_grav
    well.pcas_atma = pcas_atma
    
    
    If Len(str_AL) < 3 Then
        Set well.ESP = Nothing
    Else
        If Left(str_AL, 3) = "ESP" Then
            Set ESPsystem = ESP_decode_string(str_AL)
            Set well.ESP = ESPsystem ' link well with ESP object
            well.isCalcESPtemp = True
            well.isCalcCasing = True
            ' set separation coefficient if given
            If ESPsystem.ksep_manual_fr > 0 And ESPsystem.ksep_manual_fr <= 1 Then
                Call well.Setksep_total_d(valueManual, ESPsystem.ksep_manual_fr, 0)
            Else
                Call well.Setksep_total_d(byCorrealation, ESPsystem.ksep_manual_fr, ESPsystem.ksep_gassep_fr)
            End If
   
        ElseIf Left(str_AL, 3) = "GLV" Then
            Set GLVset = GL_decode_string(str_AL)
            Set well.valves = GLVset
        End If
         
        
        
    End If
    Set well_load_new = well
    Exit Function
err1:
    well_load_new = Array(-1, "error")
    addLogMsg "Error:well_load_new:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета линейного давления по забойному для скважины
' расчет снизу-вверх, простой и быстрый расчет
Public Function well_plin_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal pcas_atma As Double = -1, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_ESP As String = 0, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = 0, _
        Optional ByVal c_calibr_head_d As Double = 1, _
        Optional ByVal param_num As Integer = 1 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
'
' pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'           на нулевой позиции выходного массива,
'           полный набор результатов выводится в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    ' call calc method
    Call well.calc_plin_pwf_atma(pwf_atma)
    ' return results
    well_plin_pwf_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета давления на приеме по забойному для скважины
' расчет снизу-вверх, считает только участок ниже насоса
Public Function well_pintake_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal hydr_corr As H_CORRELATION = 0 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
'
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' hydr_corr  - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0,
'                   Ansari = 1,
'                   Unified = 2,
'                   Gray = 3,
'                   HagedornBrown = 4,
'                   SakharovMokhov = 5,
'                   Gas = 10.
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, , _
                             str_well, str_PVT, "0", hydr_corr)
    ' return results
    well_pintake_pwf_atma = well.calc_Pintake_pwf_atma(pwf_atma)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления по устьевому для скважины
' расчет сверху-вниз, считает быстро за счет угадывания сепарации
'                     температура только линейная или по градиенту
Public Function well_pwf_plin_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal plin_atma As Double, _
        Optional ByVal pcas_atma As Double = -1, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_ESP As String = 0, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = -1, _
        Optional ByVal p_ksep_atma As Double = 40, _
        Optional ByVal t_ksep_C As Double = 40, _
        Optional ByVal c_calibr_head_d As Double = 1, _
        Optional ByVal param_num As Integer = 5)
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' plin_atma - линейное (устьевое) давление
'
' pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция:
'                   BeggsBrill = 0;
'                   Ansari = 1;
'                   Unified = 2;
'                   Gray = 3;
'                   HagedornBrown = 4;
'                   SakharovMokhov = 5.
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' p_ksep_atma - давление сепарации
' t_ksep_C - температура сепарации
'           при расчете сверху вниз неизвестны параметры сепарации
'           если задать их явно (угадать)
'           тогда расчет упрощается и ускоряется
' c_calibr_head_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    Dim well As CWellESP
    Dim ar
    
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    ' при расчете сверху вниз задаем какие то значения сепарации наугад
    If well.is_ESP Then
        Call well.Setksep_total_d(fullyManual, ksep_fr, 0, p_ksep_atma, t_ksep_C)
        well.isCalcCasing = True
    End If
    
    ' проведем расчет
    Call well.calc_pwf_plin_atma(plin_atma, well.t_bh_C, fast:=True)
    ' в качестве результата выведем ряд расчитанных значений давления
    well_pwf_plin_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция адаптации модели скважины по данным эксплуатации
' подбирает коэффициента деградации УЭЦН и штуцера
' по замера на поверхности и на забое/приеме насоса
Public Function well_calc_calibr_head_fr( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal pdown_atma As Double, _
                     ByVal pbuf_atma As Double, _
            Optional Pdown_at_intake As Boolean = False, _
            Optional ByVal plin_atma As Double = -1, _
            Optional ByVal pcas_atma As Double = -1, _
            Optional ByVal str_well As String = WELL_DEFAULT, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal str_ESP As String = 0, _
            Optional ByVal hydr_corr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = -1, _
            Optional ByVal c_calibr_head_d As Double = 1, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' Pdown_atma - давление ниже насоса (внизу) для расчета
'     либо забойное давление (по умолчанию)
'     либо давление на приеме
'     определяется опциональным параметром Pdown_at_intake
' pbuf_atma  - буферное давление
'
' Pdown_at_intake - флаг определяет точку расчета давления
'                   ниже насоса. По умолчанию забойное
' plin_atma - линейное давление
'             если не задано штуцер не учитывается
' pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    Dim pwf_temp_atma As Double
    Dim ar
On Error GoTo err1:
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    ' calc BHP if needed from intake pressure
    If Pdown_at_intake Then
        pwf_temp_atma = well.calc_pwf_pint_atma(pdown_atma)
    Else
        pwf_temp_atma = pdown_atma
    End If
    ' проведем расчет
    If well.calc_well(plin_atma, pbuf_atma, pwf_temp_atma, pcas_atma) Then
    ' в качестве результата выведем ряд расчитанных значений давления
        well_calc_calibr_head_fr = well_out_arr(well, param_num)
    Else
        well_calc_calibr_head_fr = "c_calibr_head not found. Check log for details"
    End If
    Exit Function
err1:
    Dim errmsg As String
    errmsg = "well_calc_calibr_head_fr: error" & sDELIM & Err.Description
    addLogMsg errmsg
    well_calc_calibr_head_fr = errmsg
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления скважины по динамическому уровню
Public Function well_pwf_hdyn_atma( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal pcas_atma As Double, _
                     ByVal h_dyn_m As Double, _
            Optional ByVal str_well As String = WELL_DEFAULT, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal str_ESP As String = 0, _
            Optional ByVal hydr_corr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = 0, _
            Optional ByVal c_calibr_head_d As Double = 1, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day   - дебит жидкости, на поверхности
' fw_perc   - обводненность (объемная на поверхности)
' pcas_atma - затрубное давление
' h_dyn_m    - динамический уровень (при данном затрубном)
'
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    Set well = well_init_data(q_m3day, fw_perc, pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    Call well.calc_pwf_pcas_hdyn_atma(pcas_atma, h_dyn_m, well.t_bh_C)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    Dim ar
    well_pwf_hdyn_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета узлового анализа системы пласт - скважина - УЭЦН
' по заданным параетрам пласта, скважины и УЭЦН
' определяется рабочий дебит и забойное давление
Public Function nodal_qliq_sm3day( _
             ByVal pi_sm3dayatm As Double, _
             ByVal plin_atma As Double, _
             ByVal fw_perc As Double, _
    Optional ByVal Pres_atma = 250, _
    Optional ByVal pcas_atma As Double = 10, _
    Optional ByVal str_well As String = WELL_DEFAULT, _
    Optional ByVal str_PVT As String = PVT_DEFAULT, _
    Optional ByVal str_ESP As String = 0, _
    Optional ByVal hydr_corr As H_CORRELATION = 0, _
    Optional ByVal ksep_fr As Double = 0, _
    Optional ByVal c_calibr_head_d As Double = 1, _
    Optional ByVal param_num As Integer = 23)
' исходные параметры
' pi_sm3dayatm - коэффициент продуктивности пласта
' plin_atma - линейное давление
' fw_perc   - обводненность (объемная на поверхности)
'
' Pres_atma - пластовое давление
' pcas_atma - затрубное давление (для определения Ндин)
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr  - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    Dim PS As New CProdSys
    Dim well As CWellESP
    Dim res As New CReservoirVogel
    Dim ar
    ' init all data
    Set well = well_init_data(0, fw_perc, pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    res.InitProp Pres_atma, well.fluid.pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    Set PS.well = well
    Set PS.Reservoir = res
    
    Call PS.nodal_qliq_sm3day(plin_atma)
    
    well.qliq_sm3day = PS.qsol_scm3day
    
    Call well.calc_plin_pwf_atma(PS.p_wfsol_atma)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    
    nodal_qliq_sm3day = well_out_arr(well, param_num)
    
End Function
' ==============  функции для расчета скважины ==========================
' =====================================================================
' функция для шаблонного чтения данных по скважине в интерфейсных функциях
Private Function well_init_data(q_m3day, _
                               fw_perc, _
                      Optional pcas_atma As Double = 10, _
                      Optional str_well As String = WELL_DEFAULT, _
                      Optional str_PVT As String = PVT_DEFAULT, _
                      Optional str_ESP As String = 0, _
                      Optional hydr_corr As H_CORRELATION = 0, _
                      Optional ksep_fr As Double = 0, _
                      Optional c_calibr_head_d As Double = 1) As CWellESP
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
'
' pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации УЭЦН
' на входе данные по конструкции скважины, PVT, ЭЦН
' берутся из строк с закодированными параметрами
' следует использовать функции Encode для передачи параметров
' на выходе объект скважина с загруженными данными
    
    Dim well As New CWellESP
    Dim PVT As New CPVT
    Dim pmp As CESPpump
    Dim ESPsystem As New CESPsystemSimple
    ' read PVT data from encoded string
    Set PVT = PVT_decode_string(str_PVT)
    ' set liquid rate
    PVT.qliq_sm3day = q_m3day
    ' set water cut - correcting percent to fraction
    PVT.fw_fr = fw_perc / 100
    ' set well construction from encoded string
    ' also set temperature parameters (BHT and WHT)
    Set well = well_decode_string(str_well)
    ' link well with fluid object
    Set well.fluid = PVT
    ' set casing pressure
    well.pcas_atma = pcas_atma
    ' set hydraulic correlation
    well.hydraulic_correlation = hydr_corr
    ' set temperature model with temperature gradient
    well.temp_correlation = GeoGradTemp
    ' read ESP object from encoded string
    If Len(str_ESP) < 3 Then
        Set well.ESP = Nothing
    Else
        Set ESPsystem = ESP_decode_string(str_ESP)
        ' link well with ESP object
        Set well.ESP = ESPsystem
        ' set correction factor for ESP
        well.ESP.c_calibr_head = c_calibr_head_d
        ' set separation coefficient if given
        If ksep_fr > 0 And ksep_fr <= 1 Then
            Call well.Setksep_total_d(valueManual, ksep_fr, 0)
        Else
            Call well.Setksep_total_d(byCorrealation, ksep_fr)
        End If
    End If
    
    
    ' return result well object
    Set well_init_data = well
End Function
' function prepares output array for public functions
' подготовим данные для вывода
Private Function well_out_arr(well, _
                     Optional ByVal FirsrCol As Integer = 0, _
                     Optional ByVal num_points As Integer = 21)
' данные выводятся в одну линию, чтобы, хотя бы теоретически, можно было вывести данные по скважине в таблице
' во второй строке выводятся подписи параметров, если необходимо
' первый параметр настраиваемый
    Dim ar1, ar2
    Dim res As New Dictionary
    Const arr_len = 25
On Error GoTo err1:
    ReDim ar1(arr_len)
    ReDim ar2(arr_len)
    Dim i As Integer
    With well
         res.Add "p_line_atma", .p_line_atma                                  'i=1
         res.Add "pbuf_atma", .pbuf_atma                                    'i=2
         res.Add "p_dis_atma", .p_dis_atma                                    'i=3
         res.Add "p_int_atma", .p_int_atma                                    'i=4
         res.Add "pwf_atma", .pwf_atma                                      'i=5
         res.Add "qliq_sm3day", .qliq_sm3day                                'i=23
         res.Add "fw_perc", .fw_perc                                        'i=24
         res.Add "t_bh_C", .t_bh_C                                            'i=20
         res.Add "t_wh_C", .t_wh_C                                            'i=17
         res.Add "t_surf_C", .t_surf_C                                        'i=16
         res.Add "t_dis_C", .t_dis_C                                          'i=18
         res.Add "t_int_C", .t_int_C                                    'i=19
         res.Add "choke.c_degrad_fr", .choke.c_degrad_fr                      'i=22
         res.Add "pcas_atma", .pcas_atma                                    'i=6
         res.Add "h_dyn_m", .h_dyn_m                                          'i=7
         If .is_ESP Then
            res.Add "ESP.power_CS_calc_W", .ESP.power_CS_calc_W                'i=8
            res.Add "ESP.freq_Hz", .ESP.freq_Hz                                'i=9
            res.Add "ESP.I_A", .ESP.I_A                                        'i=10
            res.Add "ESP.load_fr", .ESP.load_fr                                'i=11
            res.Add "ESP.ESP_pump.eff_ESP_d", .ESP.ESP_pump.eff_ESP_d    'i=12
            res.Add "ESP.ksep_total_fr", .ESP.ksep_total_fr                      'i=13
            res.Add "ESP.ksep_nat_fr", .ESP.ksep_nat_fr                          'i=14
            res.Add "ESP.ksep_gassep_fr", .ESP.ksep_gassep_fr                    'i=15
            res.Add "ESP.c_calibr_head", .ESP.c_calibr_head                            'i=21
        End If
        
        For i = 1 To res.Count
            ar1(i) = res.Items()(i - 1): ar2(i) = res.Keys()(i - 1)
        Next i
        ' first column can have any number
        ' set it here
        ar1(0) = ar1(FirsrCol): ar2(0) = ar2(FirsrCol)
        
        ' выведем дополнительно спасенные кривые по скважине
        Dim arr()
        
        Dim crv1 As CInterpolation
        Dim crv_name As String
        Dim offset As Integer
        Dim n As Integer
        
        offset = 3
        
        ReDim Preserve arr(num_points + offset, arr_len)
        For i = LBound(ar1) To UBound(ar1)
            arr(0, i) = ar1(i)
            arr(1, i) = ar2(i)
            arr(2, i) = i
        Next i
        
        Set crv1 = .curve(str_PtubHmes_curve).ClonePointsToNum(num_points)
        arr(offset, 0) = "num"
        arr(offset, 1) = crv1.xName
        arr(offset, 2) = crv1.yName
        For i = 1 To num_points
            arr(offset + i, 0) = i
            arr(offset + i, 1) = crv1.pointX(i)
            arr(offset + i, 2) = crv1.PointY(i)
        Next i
        n = 2
        ' use copy paste below :)
        
        crv_name = "c_udl_m": n = n + 1
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
        crv_name = "c_Diam": n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
        crv_name = "c_Theta": n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
        crv_name = str_TtubHmes_curve: n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
                                            
        crv_name = str_HlHmes_curve: n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
                                                   
                                            
        crv_name = "Pcas_Hd": n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
         crv_name = str_HLcasHmes_curve: n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
         crv_name = str_TambHmes_curve: n = n + 2
                                            Set crv1 = .curve(crv_name).ClonePointsToNum(num_points)
                                            arr(offset, n) = crv1.xName
                                            arr(offset, n + 1) = crv1.yName
                                            For i = 1 To num_points
                                                arr(offset + i, n) = crv1.pointX(i)
                                                arr(offset + i, n + 1) = crv1.PointY(i)
                                            Next i
       
        
        
        
        well_out_arr = arr
        
    ' можно еще добавить сюда вывод кривых распределения давления и температуры по стволу и еще 4 параметров  (потом)
    End With
    Exit Function
err1:
    Resume Next
End Function

